; BANK0/BANK8 COMMON PATCH CODE

	INCLUDE	"gfss.iz"

	ORG	0BD90H

; 0BD90H
	ASSERT  GFSS_SOUND_DRIVER == $, fix gfss.inc

	LD	A,08H ; SOUND DRIVER BANK
	LD	(07000H),A

	CALL	0B97CH

	XOR	A
	LD	(07000H),A
	RET

; 0BD9DH
	ASSERT GFSS_H_KEYI == $, fix gfss.inc
	ASSERT GFSS_CURRENT_BANK == $ + 1, fix gfss.inc
h_keyi_entry_implement:
	LD	A,00H ; CURRENT BANK

	PUSH	AF
	CALL	.handler
	POP	AF

	LD	(07000H),A

	RET

.handler:

	LD	HL, (0006H)	; L:VDP READ PORT / H:VDP WRITE PORT
	INC	L		; VDP READ PORT 1
	INC	H		; VDP WRITE PORT 1

	LD	C, H

	; SELECT STATUS #1
	LD	DE, 018FH
	CALL	.WRTVDPD

	; READ STATUS #1
	; GET AND CLEAR HSYNC STATE
	LD	C, L
	IN	A, (C)
	LD	C, H

	; SELECT STATUS #0

	DEC	D
	CALL	.WRTVDPD

	RRA

	RET	NC

	LD	A, (GFSS_HSYNC_LINE)
	CP	GFSS_TOPY
	JR	NZ, .vsyncx

	; SET HSYNC INTERRUPT LINE TO SCREEN BOTTOM
	LD	A, (GFSS_SCROLL_OFFSET)
	ADD	192 - 16
	LD	(GFSS_HSYNC_LINE), A
	LD	E, 19 + 80H
	CALL	.WRTVDPA

	; SET SCROLL OFFSET
	LD	A, (GFSS_SCROLL_OFFSET)
	LD	E, 23 + 80H
	CALL	.WRTVDPA

	; SET NAME TABLE BASE
	LD	A, (GFSS_NAME_TABLE_NEXT)
	LD	E, 2 + 80H
	CALL	.WRTVDPA

	; SET BACK DROP COLOR TO BLACK
	;LD	DE, 0087H
	;CALL	.WRTVDPD

	; ENABLE SPRITE DISPLAY
	LD	A, (RG8SAV)
	AND	0FDH
	JP	.WRTVDP8


.vsyncx:

	; SET HSYNC INTERRUPT LINE TO GFSS_TOPY
	LD	A, GFSS_TOPY
	LD	(GFSS_HSYNC_LINE), A
	LD	E, 19+80H
	CALL	.WRTVDPA


	LD	A, (0C44DH) ; TITLE FLAG
	DEC	A
	JR	Z, .skip_title

	; SET COLOR TABLE TO BLANK
	LD	DE, 018AH
	CALL	.WRTVDPD

	XOR	A
.skip_title:
	; SET SCROLL OFFSET
	LD	E, 23 + 80H
	CALL	.WRTVDPA


	; DISABLE SPRITE DISPLAY
	LD	A, (RG8SAV)
	OR	2
	CALL	.WRTVDP8

	; SET BACK DROP COLOR TO WHITE
	;LD	DE, 0F87H
	;CALL	.WRTVDPD

	LD	A, (0CCDCH) ; FIGHTER SELECT FLAG
	LD	E,A
	LD	A, (0C496H) ; HIGH SPEED SCROLL FLAG
	OR	E
	LD	DE, (0C47CH) ; BOSS/ENDING FLAG
	OR	D
	OR	E

	LD	A, 0
	JR	NZ, .scroll_fixed

	LD	A, (0C476H)
	RRCA
	AND	07H
	INC	A
.scroll_fixed:

	LD	(GFSS_SCROLL_OFFSET), A

	XOR	A
	LD	(07000H), A

	JP	GFSS_SET_SPRITE

.WRTVDP8:
	LD	E, 88H
.WRTVDPA:
	LD	D, A
.WRTVDPD:
	OUT	(C), D
  IFNDEF VDPNOWAIT
	NOP
	NOP
  ENDIF
	OUT	(C), E
	RET

	DS	0BE48H - $

	ASSERT GFSS_SETRD == $, fix gfss.inc

setrd_ex_implement:

	DI

 ;DEFINE NEOSMA20
 IFDEF NEOSMA20

	PUSH	BC

	LD	A, (0007H)
	INC	A
	LD	C, A

	OUT	(C), L

	LD	A, H
	AND	3FH
	OUT	(C), A

	POP	BC

 ELSE

	LD	A, L
	OUT	(99H), A
	LD	A, H
	AND	3FH
	OUT	(99H), A

 ENDIF

	RET

	DS	0BE58H - $

	ASSERT GFSS_SETWRT == $, fix gfss.inc

setwrt_ex_implement:

	DI

 IFDEF NEOSMA20

	PUSH	BC

	LD	A, (0007H)
	INC	A
	LD	C, A

	OUT	(C), L

	LD	A, H
	AND	3FH
	OR	40H
	OUT	(C), A

	POP	BC

 ELSE

	LD	A, L
	OUT	(99H), A
	LD	A, H
	AND	3FH
	OR	40H
	OUT	(99H), A

 ENDIF

	RET

	DS	0BE6BH - $
